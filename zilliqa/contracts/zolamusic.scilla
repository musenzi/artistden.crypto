(***************************************************)
(*                 Scilla version                  *)
(***************************************************)

scilla_version 0

(***************************************************)
(*               Associated library                *)
(***************************************************)
library Zola_Music

let one_msg =
  fun (msg : Message) =>
  let nil_msg = Nil {Message} in
    Cons {Message} msg nil_msg

let not_empty = 
  fun (str : String) =>
  let empt = "" in
  builtin eq empt str
    
let pay_failure = Int32 1 
let pay_success = Int32 2
let already_backed_code = Int32 3
let funds_withdrawn = Int32 42
let cannot_get_funds = Int32 404
let not_owner_code = Int32 400
let song_added = Int32 92
let song_removed = Int32 91
let fan_added = Int32 96
let mail_changed = Int32 993
let song_add_fail = Int32 20
let song_support_fail = Int32 403
let convert_zil = Uint128 1000000000000


(***************************************************)
(*             The contract definition             *)
(***************************************************)
contract Zola_Music

(*  Parameters *)
(owner:ByStr20,
description:String,
artist:String,
origin_mail :String)

(* Mutable fields *)
field b_songs : Map String String = Emp String String
field songs_support : Map String Uint128 = Emp String Uint128
field backers : Map ByStr20 Uint128 = Emp ByStr20 Uint128
field fans : Map String Uint128 = Emp String Uint128
field funded : Bool = False
field total_funded : Uint128 = Uint128 0
field curr_email : String = origin_mail

(*Events*)
procedure AddSongEvent (error_code : Int32)
  success = builtin eq error_code song_added;
  match success with
  | True =>
      e = {_eventname : "AddSongSuccess"; caller : _sender;
         amount : _amount; code : error_code};
         event e
  | False =>
      e = {_eventname : "AddSongFailed"; caller : _sender;
         amount : _amount; code : error_code};
         event e
  end
end
procedure RemoveSongEvent (error_code : Int32)
  success = builtin eq error_code song_removed;
  match success with
  | True =>
      e = {_eventname : "RemoveSongSuccess"; caller : _sender;
         amount : _amount; code : error_code};
         event e
  | False =>
      e = {_eventname : "RemoveSongFailed"; caller : _sender;
         amount : _amount; code : error_code};
         event e
  end
end
procedure DonationEvent (failure : Bool)
  match failure with
  | True =>
      e = {_eventname : "DonationSuccess"; donor : _sender;
         amount : _amount; code : already_backed_code};
         event e
  | False =>
      e = {_eventname : "DonationSuccess"; donor : _sender;
         amount : _amount; code : pay_success};
         event e
  end
end

procedure WithdrawFundsFailure (error_code : Int32)
  e = {_eventname : "GetFundsFailure"; caller : _sender;
       amount : _amount; code : error_code};
  event e
end

procedure SupportSongEvent (error_code : Int32)
  success = builtin eq error_code pay_success;
  match success with
  | True =>
      e = {_eventname : "SupportSongSuccess"; supporter : _sender;
         amount : _amount; code : error_code};
         event e
  | False =>
      e = {_eventname : "SupportSongFailed"; supporter : _sender;
         amount : _amount; code : error_code};
         event e
  end
end

procedure ChangeMailEvent (error_code : Int32)
  success = builtin eq error_code mail_changed;
  match success with
  | True =>
      e = {_eventname : "ChangeMailEvent"; supporter : _sender;
         amount : _amount; code : error_code};
         event e
  | False =>
      e = {_eventname : "ChangeMailEvent"; supporter : _sender;
         amount : _amount; code : error_code};
         event e
  end
end

(*Private methods or smth alike*)
procedure PerformWithdrawFunds()
  bal <- _balance;
  tt = True;
  funded := tt;
  
  msg = { _tag : ""; _recipient : owner; _amount : bal; code : funds_withdrawn};
  msgs = one_msg msg;
  send msgs
end

procedure AddFan(name : String)
  fo <- exists fans[name];
  f <- fans[name];
  
  match f with 
    | None =>
      fans[name] := _amount;
      msg = { _tag : ""; _recipient : _sender; _amount: Uint128 0; code : fan_added};
      msgs = one_msg msg;
      send msgs
    | Some fan =>
      all = builtin add _amount fan;
      fans[name] := all;
      msg = { _tag : ""; _recipient : _sender; _amount: Uint128 0; code : pay_success};
      msgs = one_msg msg;
      send msgs 
  end
end

procedure SendToSong( name : String)
  s <- songs_support[name];
  match s with
    | None =>
      SupportSongEvent song_support_fail
    | Some b =>
      accept;
      am = _amount;
      zila = builtin div am convert_zil;
      tot = builtin add b zila;
      songs_support[name] := tot; 
      bal <- _balance;
      zbal = builtin div bal convert_zil;
      total_funded := zbal; 
      SupportSongEvent pay_success
  end
end

(*Transitions*)

(*[OwnerOnly]*)
(*Add Song with title and lyrics should point to a resource like url*)
transition AddSong( title: String, lyrics : String )
  is_owner = builtin eq owner _sender;
  match is_owner with
  | False =>
    AddSongEvent not_owner_code
  | True =>
    b_songs[title] := lyrics;
    zer = Uint128 0;
    songs_support[title] := zer;
    AddSongEvent song_added
  end
end

(*[OwnerOnly]*)
(*Removes song by title*)
transition RemoveSong( title: String)
  is_owner = builtin eq owner _sender;
  match is_owner with
  | False =>
    RemoveSongEvent not_owner_code
  | True =>
    delete b_songs[title];
    RemoveSongEvent song_added
  end
end

(*[OwnerOnly]*)
(*Changes curr_mail*)
transition ChangeMail( mail: String)
  is_owner = builtin eq owner _sender;
  match is_owner with
  | False =>
    ChangeMailEvent not_owner_code
  | True =>
    m = mail;
    curr_email := m;
    ChangeMailEvent mail_changed
  end
end

(*[OwnerOnly]*)
(*Withdraw all funds from contract*)
transition WithdrawFunds ()
  is_owner = builtin eq owner _sender;
  match is_owner with
  | False =>
    WithdrawFundsFailure not_owner_code
  | True =>
    PerformWithdrawFunds
  end
end

transition Donate ( name : String )
  accept;
  o <- exists backers[_sender];
  b <- backers[_sender];
  AddFan name;
  bal <- _balance;
  newtot = builtin div bal convert_zil;
  total_funded := newtot;    
  match b with
  | None =>
    backers[_sender] := _amount;
    DonationEvent o
  | Some b =>
    tot = builtin add b _amount;
    backers[_sender] := tot;
    DonationEvent o
  end
end

(*Support Song by name*)
transition SupportSong ( name : String )
  o <- exists songs_support[name];
  match o with 
  | True =>
    SendToSong name
  | False =>
    SupportSongEvent song_support_fail
  end
end